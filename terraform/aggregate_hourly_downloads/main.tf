terraform {
  required_version = "~> 1.13"
  required_providers {
    google = {
      source  = "hashicorp/google"
      version = "~> 7.2"
    }
  }
  backend "gcs" {
    prefix = "stats-aggregate-hourly-downloads"
  }
}

provider "google" {
  project = var.gcp_project_id # default inherited by all resources
  region  = var.gcp_region     # default inherited by all resources
}

### service account ###

resource "google_service_account" "account" {
  account_id   = "stats-downloads"
  display_name = "Service account to deploy aggregate-hourly-downloads cloud function"
}

resource "google_cloudfunctions2_function_iam_member" "invoker" {
  cloud_function = google_cloudfunctions2_function.function.name
  role           = "roles/cloudfunctions.invoker"
  member         = "serviceAccount:${google_service_account.account.email}"
}

resource "google_cloud_run_service_iam_member" "cloud_run_invoker" {
  service = google_cloudfunctions2_function.function.name
  role    = "roles/run.invoker"
  member  = "serviceAccount:${google_service_account.account.email}"
}

resource "google_secret_manager_secret_iam_member" "read_db" {
  secret_id = var.read_db_pw_secret_name
  role      = "roles/secretmanager.secretAccessor"
  member    = "serviceAccount:${google_service_account.account.email}"
}

resource "google_secret_manager_secret_iam_member" "write_db" {
  secret_id = var.write_db_pw_secret_name
  role      = "roles/secretmanager.secretAccessor"
  member    = "serviceAccount:${google_service_account.account.email}"
}

resource "google_project_iam_member" "bq_jobs_user" {
  project = var.gcp_project_id
  role    = "roles/bigquery.jobUser"
  member  = "serviceAccount:${google_service_account.account.email}"
}

resource "google_bigquery_dataset_iam_member" "viewer" {
  dataset_id = "arxiv_logs"
  role       = "roles/bigquery.dataViewer"
  member     = "serviceAccount:${google_service_account.account.email}"
}

resource "google_project_iam_member" "cloudsql_client" {
  project = var.gcp_project_id
  role    = "roles/cloudsql.client"
  member  = "serviceAccount:${google_service_account.account.email}"
}

### cloud function ###

resource "google_cloudfunctions2_function" "function" {
  name        = "stats-aggregate-hourly-downloads" # name should use kebab-case so generated Cloud Run service name will be the same
  location    = var.gcp_region                     # needs to be explicitly declared for Cloud Run
  description = "Cloud function to parse download data from logs and persist to a database"

  build_config {
    runtime     = "python313"
    entry_point = "aggregate_hourly_downloads"
    source {
      storage_source {
        bucket = google_storage_bucket.bucket.name
        object = google_storage_bucket_object.object.name
      }
    }
  }

  service_config {
    min_instance_count    = 0 # cold starts to reduce costs
    available_memory      = "6Gi"
    available_cpu         = "2" # must be explicitly set if memory>4Gi
    timeout_seconds       = 540 # 9 minutes is the maximum allowed for pubsub triggered functions
    ingress_settings      = "ALLOW_INTERNAL_ONLY"
    service_account_email = google_service_account.account.email
    environment_variables = {
      ENV                          = var.env
      READ_DB__DRIVERNAME          = var.read_db_drivername
      READ_DB__USERNAME            = var.read_db_username
      READ_DB__DATABASE            = var.read_db_database
      READ_DB__QUERY__UNIX_SOCKET  = var.read_db_unix_socket
      WRITE_DB__DRIVERNAME         = var.write_db_drivername
      WRITE_DB__USERNAME           = var.write_db_username
      WRITE_DB__DATABASE           = var.write_db_database
      WRITE_DB__QUERY__UNIX_SOCKET = var.write_db_unix_socket
    }
    secret_environment_variables {
      key        = "READ_DB__PASSWORD"
      project_id = var.gcp_project_id
      secret     = var.read_db_pw_secret_name
      version    = "latest"
    }
    secret_environment_variables {
      key        = "WRITE_DB__PASSWORD"
      project_id = var.gcp_project_id
      secret     = var.write_db_pw_secret_name
      version    = "latest"
    }
  }

  event_trigger {
    trigger_region = "us-central1"
    event_type     = "google.cloud.pubsub.topic.v1.messagePublished"
    pubsub_topic   = google_pubsub_topic.topic.id
    retry_policy   = "RETRY_POLICY_RETRY"
  }
}

resource "google_storage_bucket" "bucket" {
  name                        = lower("${var.env}-stats-aggregate-hourly-downloads") # prefixed with env because buckets must be globally unique
  location                    = "US"
  uniform_bucket_level_access = true
}

resource "google_storage_bucket_object" "object" {
  name   = "aggregate-hourly-downloads-src-${var.commit_sha}.zip"
  bucket = google_storage_bucket.bucket.name
  source = "src.zip"
}

### scheduled pubsub ###

resource "google_pubsub_topic" "topic" {
  name = "stats-aggregate-hourly-downloads"
}

resource "google_cloud_scheduler_job" "invoke_cloud_function" {
  name        = "invoke-stats-aggregate-hourly-downloads"
  description = "Publish an hourly message to invoke the aggregate-hourly-downloads cloud function"
  schedule    = "1 * * * *" # every hour at one minute past
  time_zone   = "UTC"

  pubsub_target {
    topic_name = google_pubsub_topic.topic.id
    data       = base64encode("invoke")
  }
}


### alerting ###

resource "google_monitoring_alert_policy" "cloud_run_error_alert" {
  display_name = "${google_cloudfunctions2_function.function.name} NoRetryError"
  combiner     = "OR"
  severity     = "ERROR"

  conditions {
    display_name = "Cloud Run NoRetryError log"
    condition_matched_log {
      filter = "resource.type=\"cloud_run_revision\" AND resource.labels.service_name=\"${google_cloudfunctions2_function.function.name}\" AND jsonPayload.message=~\"A NoRetry exception has been raised\""
    }
  }

  alert_strategy {
    notification_rate_limit {
      period = "300s" # limit notifications to every 5 minutes
    }
  }

  notification_channels = [
    "projects/${var.gcp_project_id}/notificationChannels/${var.slack_channel_id}"
  ]

  documentation {
    content   = "Cloud Run service ${google_cloudfunctions2_function.function.name} raised a NoRetryError and will not retry - see logs, fix the problem, and manually run the function to patch data as needed."
    mime_type = "text/markdown"
  }
}

resource "google_monitoring_alert_policy" "cloud_run_oom_alert" {
  display_name = "${google_cloudfunctions2_function.function.name} OOM"
  combiner     = "OR"
  severity     = "ERROR"

  conditions {
    display_name = "Cloud Run OOM log"
    condition_matched_log {
      filter = "resource.type=\"cloud_run_revision\" AND resource.labels.service_name=\"${google_cloudfunctions2_function.function.name}\" AND severity=ERROR AND textPayload=~\"Memory limit of.*exceeded\""
    }
  }

  alert_strategy {
    notification_rate_limit {
      period = "300s" # limit notifications to every 5 minutes
    }
  }

  notification_channels = [
    "projects/${var.gcp_project_id}/notificationChannels/${var.slack_channel_id}"
  ]

  documentation {
    content   = "Cloud Run service ${google_cloudfunctions2_function.function.name} was killed for exceeding its memory limit (OOM) - see logs to determine whether the container's available_memory needs to be increased or whether the function is leaking/overusing memory."
    mime_type = "text/markdown"
  }
}
